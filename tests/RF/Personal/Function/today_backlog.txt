*** Settings ***
Library           Selenium2Library
Resource          ../Conf/personal.txt
Resource          ../Conf/today_backlog.txt

*** Keywords ***
I wait for actions in Today Backlog
    Wait Until Page Contains    ${ACTION1.title}    timeout=${TIMEOUT}

I click add button
    Click Button    td_b_action_add

I click edit button
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Click Element    ${ACTION_VIEW.edit_button}_${key}

I click delete button
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Click Element    ${ACTION_VIEW.delete_button}_${key}

I click edit link
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Click Link    ${ACTION_VIEW.edit_link}_${key}

I click save button
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Click Button    ${ACTION_EDIT.save}_${key}

I click close button
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Click Button    ${ACTION_EDIT.close}_${key}

I confirm delete modal
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Wait Until Element Is Visible    ${ACTION_MODAL.yes}_${key}
    Click Button    ${ACTION_MODAL.yes}_${key}

I do not confirm delete modal
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Wait Until Element Is Visible    ${ACTION_MODAL.no}_${key}
    Click Button    ${ACTION_MODAL.no}_${key}

I close delete modal
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Wait Until Element Is Visible    ${ACTION_MODAL.close}_${key}
    Click Button    ${ACTION_MODAL.close}_${key}

Following elements of action are displayed
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Page Should Contain    [${ENV}] ${action.title}
    Element Should Contain    ${ACTION_VIEW.todo_pill}_${key}    ${action.todo}
    ${total}=    Evaluate    ${action.todo} + ${action.done}
    Element Should Contain    ${ACTION_VIEW.done_pill}_${key}    ${action.done} / ${total}
    Element Should Contain    ${ACTION_VIEW.date_pill}_${key}    ${action.date}

Action form should contain
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Textfield Value Should Be    ${ACTION_EDIT.title}_${key}    [${ENV}] ${action.title}
    Date should be    ${ACTION_EDIT.date}_${key}    ${action.date}
    Element should have model value    ${ACTION_EDIT.todo}_${key}    ${action.todo}
    Element should have model value    ${ACTION_EDIT.done}_${key}    ${action.done}
    Textarea Value Should Be    ${ACTION_EDIT.desc}_${key}    ${action.description}

I input values in action form
    [Arguments]    &{action}
    ${key} =    Get key action from li id    td    ${action.order}
    Input Text    ${ACTION_EDIT.title}_${key}    [${ENV}] ${action.title}
    Input Text    xpath=(//my-date-picker[@id='${ACTION_EDIT.date}_${key}'])[1]//input    ${action.date}
    Input Text    ${ACTION_EDIT.todo}_${key}    ${action.todo}
    Input Text    ${ACTION_EDIT.done}_${key}    ${action.done}
    Comment    Textarea Value Should Be    ${ACTION_EDIT.desc}_${key}    ${action.description}
