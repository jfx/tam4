*** Settings ***
Library           OperatingSystem
Library           Process
Library           Selenium2Library
Library           String
Resource          ../Conf/common.txt

*** Keywords ***
Set URL
    [Arguments]    ${env_url}
    Set Global Variable    ${URL}    ${env_url}

I go to site
    Run Keyword If    '''${ENV}''' == 'REMOTE'    Set URL    ${URL_REMOTE}
    Run Keyword Unless    '''${ENV}''' == 'REMOTE'    Set URL    ${URL_LOCAL}
    Run Keyword If    ${GRID}    Open Browser    ${URL}    ${BROWSER}    remote_url=${HUB}
    Run Keyword Unless    ${GRID}    Open Browser    ${URL}    ${BROWSER}
    Maximize Browser Window

Element should have model value
    [Arguments]    ${id}    ${value}
    ${attr-model}=    Get Element Attribute    ${id}@ng-reflect-model
    Should Be Equal    ${attr-model}    ${value}

Date should be
    [Arguments]    ${id}    ${value}
    ${attr-value}=    Get Element Attribute    xpath=(//my-date-picker[@id='${id}'])@ng-reflect-sel-date
    Should Be Equal    ${attr-value}    ${value}

Load DB
    Run Keyword If    '${ENV}' == 'LOCAL'    Load local DB
    Run Keyword If    '${ENV}' == 'REMOTE'    Load remote DB

Load local DB
    Log    -- Load Local DB --
    Copy File    ${DATA_DIR}/local-ref.json    ${DATA_DIR}/local-test.json

Load remote DB
    Log    -- Load Remote DB --
    ${rc} =    Run And Return Rc    ${BIN_DIR}/loadRemoteDB.sh
    Should Be Equal As Integers    ${rc}    0

Teardown
    Log    -- Teardown --
    Close All Browsers
    Terminate All Processes
    : FOR    ${tag}    IN    @{TEST TAGS}
    \    Run Keyword If    '${tag}' == 'EDIT'    Load DB
